{{fbdoc item="title" value="AND"}}----
Parameter to the ##[[KeyPgPutgraphics|Put]]## graphics statement which uses a bit-wise ##[[KeyPgOpAnd|And]]## as the blitting method

{{fbdoc item="syntax"}}##
	**Put** [ //target//, ] [ STEP ] ( //x//,//y// ), //source// [ ,( //x1//,//y1// )-( //x2//,//y2// ) ], **And**
##
{{fbdoc item="param"}}
	##**And**##
		Required.

{{fbdoc item="desc"}}
	The ##**And**## method combines each source pixel with the corresponding destination pixel, using the bit-wise ##[[KeyPgOpAnd|And]]## function.  The result of this is output as the destination pixel.
	This method works in all graphics modes.  There is no mask color, although color values with all bits set (##255## for 8-bit palette modes, or ##[[KeyPgRgba|RGBA]](255, 255, 255, 255)## in full-color modes) will have no effect, because of the behavior of ##[[KeyPgOpAnd|And]]##.
	
	In full-color modes, each component (red, green, blue and alpha) is kept in a discrete set of bits, so the operation can be made to only affect some of the channels, by making sure the all the values of the other channels are set to ##255##.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/put-and.bas"}}%%(freebasic)
''open a graphics window
ScreenRes 320, 200, 16
line (0, 0)-(319, 199), RGB(255, 255, 255), bf

''create 3 sprites containing cyan, magenta and yellow circles
Const As Integer r = 32
dim as any ptr cc, cm, cy
cc = ImageCreate(r * 2 + 1, r * 2 + 1, RGBA(255, 255, 255, 255))
cm = ImageCreate(r * 2 + 1, r * 2 + 1, RGBA(255, 255, 255, 255))
cy = ImageCreate(r * 2 + 1, r * 2 + 1, RGBA(255, 255, 255, 255))
Circle cc, (r, r), r, RGB(0, 255, 255), , , 1, f
Circle cm, (r, r), r, RGB(255, 0, 255), , , 1, f
Circle cy, (r, r), r, RGB(255, 255, 0), , , 1, f

''put the three sprites, overlapping each other in the middle
Put (146 - r, 108 - r), cc, and
Put (174 - r, 108 - r), cm, and
Put (160 - r,  84 - r), cy, and

''free the memory used by the sprites
ImageDestroy cc
ImageDestroy cm
ImageDestroy cy

''pause the program before closing
Sleep
%%
{{image class="center" title="Put And example output" url="/images/put-and.png"}}
{{fbdoc item="diff"}}
	- None

{{fbdoc item="see"}}
	- ##[[KeyPgOpAnd|And]]##
	- ##[[KeyPgPutgraphics|Put (Graphics)]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}
