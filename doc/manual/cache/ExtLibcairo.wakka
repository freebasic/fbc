{{fbdoc item="title" value="Cairo"}}----
2D graphics library with support for multiple output devices.

Website: http://www.cairographics.org
Platforms supported: ""Win32"", Linux
Headers to include: cairo/cairo.bi
Header version: 1.2.6

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/libraries/cairo.bas"}}%%(freebasic)
'' Cairo clock. Translated from the C example written by Writser Cleveringa

#include once "cairo/cairo.bi"
#include once "datetime.bi"

const SCREEN_W = 400
const SCREEN_H = 300

const PI = 3.14159265358979323846#

screenres SCREEN_W, SCREEN_H, 32

''
'' Create a cairo drawing context, using the FB screen as surface.
''
dim as cairo_surface_t ptr surface = _
	cairo_image_surface_create_for_data(screenptr(), _
			CAIRO_FORMAT_ARGB32, SCREEN_W, SCREEN_H, _
			SCREEN_W * sizeof(integer))

dim as cairo_t ptr c = cairo_create(surface)
cairo_scale(c, SCREEN_W, SCREEN_H)

do
	screenlock()

	''
	'' Draw a clock
	''

	'' compute the angles for the indicators of our clock
	dim as double minutes = minute(now()) * PI / 30
	dim as double hours = hour(now()) * PI / 6
	dim as double seconds = second(now()) * PI / 30

	'' Draw the entire context white.
	cairo_set_source_rgba(c, 1, 1, 1, 1)
	cairo_paint(c)

	'' Who doesn't want all those nice line settings :)
	cairo_set_line_cap(c, CAIRO_LINE_CAP_ROUND)
	cairo_set_line_width(c, 0.1)

	'' Translate to the center of the rendering context,
	'' and draw a black clock outline.
	cairo_set_source_rgba(c, 0, 0, 0, 1)
	cairo_translate(c, 0.5, 0.5)
	cairo_arc(c, 0, 0, 0.4, 0, PI * 2)
	cairo_stroke(c)

	'' Draw a white dot on the current second.
	cairo_set_source_rgba(c, 1, 1, 1, 0.6)
	cairo_arc(c, sin(seconds) * 0.4, -cos(seconds) * 0.4, 0.05, 0, PI * 2)
	cairo_fill(c)

	'' Draw the minutes indicator
	cairo_set_source_rgba(c, 0.2, 0.2, 1, 0.6)
	cairo_move_to(c, 0, 0)
	cairo_line_to(c, sin(minutes) * 0.4, -cos(minutes) * 0.4)
	cairo_stroke(c)

	'' Draw the hours indicator
	cairo_move_to(c, 0, 0)
	cairo_line_to(c, sin(hours) * 0.2, -cos(hours) * 0.2)
	cairo_stroke(c)

	screenunlock()

	sleep 1000
loop while (len(inkey()) = 0)

'' Clean up the cairo context
cairo_destroy(c)
%%


{{fbdoc item="back" value="ExtLibTOC|External Library Table of Contents"}}