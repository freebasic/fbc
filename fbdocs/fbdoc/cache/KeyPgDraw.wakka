{{fbdoc item="title" value="DRAW"}}----
Statement for sequenced pixel plotting

{{fbdoc item="syntax"}}##
	**Draw** [//target//,] //cmd//
##
{{fbdoc item="param"}}
	##//target//##
		the buffer to draw on
	##//cmd//##
		a string containing the sequence of commands

{{fbdoc item="desc"}}
	Drawing will take place onto the current work page set via ##[[KeyPgScreenset ScreenSet]]## or onto the ##//target//## ##[[KeyPgGetgraphics Get]]##/##[[KeyPgPutgraphics Put]]## buffer if specified.
	The ##**Draw**## statement can be used to issue several drawing commands all at once; it is useful to quickly draw figures. The command string accepts the following commands:

	Commands to plot pixels:
	{{table columns="2" cellpadding="1" cells="Command;Description;###;Commands to plot pixels:;B;Optional prefix: move but do not draw.;N;Optional prefix: draw but do not move.;Mx,y;Move to specified screen location. if + or - precedes x, movement is relative to current cursor position.;U[n];Move n units up. If n is omitted, 1 is assumed.;D[n];Move n units down. If n is omitted, 1 is assumed.;L[n];Move n units left. If n is omitted, 1 is assumed.;R[n];Move n units right. If n is omitted, 1 is assumed.;E[n];Move n units up and right. If n is omitted, 1 is assumed.;F[n];Move n units down and right. If n is omitted, 1 is assumed.;G[n];Move n units down and left. If n is omitted, 1 is assumed.;H[n];Move n units up and left. If n is omitted, 1 is assumed.;###;Commands to color:;Cn;Changes current foreground color to n.;Pp,b;Flood fills region with border color b with color p.;###;Commands to scale and rotate:;Sn;Sets the current unit length, default is 4.  A unit length of 4 is equal to 1 pixel.;An;Rotate n*90 degrees (n ranges 0-3).;TAn;Rotate n degrees (n ranges 0-359);###;Extra commands:;Xp;Executes commands at address p."}}

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/draw.bas"}}%%(freebasic)
'' set the screen mode to 320 * 200, 256 color
Screen 13

'' start in the center of the screen
Draw "BM160,100"

'' Draw a box
Draw "U10 R5 D10 L5"
Sleep
%%

{{fbdoc item="filename" value="examples/manual/gfx/draw2.bas"}}%%(freebasic)
dim as string fill, angle, pattern
dim as integer i, a, c

pattern = ("X" & varptr(angle)) _
	    & "C15" _
	      "M+100,+10" _
	      "M +15,-10" _
	      "M -15,-10" _
	      "M-100,+10" _
	    & "BM+100,0" & ("X" & varptr(fill)) & "BM-100,0"


screenres 320, 240, 8

draw "BM 160, 120"
a = 0: c = 32
for i = 1 to 24
	
	angle = "TA" & a
	fill = "P" & c & ",15"
	a += 15: c += 1
	
	draw pattern
	sleep 100
	
next i

sleep
%%
{{image class="center" title="Draw example output" url="/images/draw2.png"}}

{{fbdoc item="diff"}}
	- QB used the special pointer keyword VARPTR$ with the ##Xp## command.

{{fbdoc item="see"}}
	- ##[[KeyPgDrawString Draw String]]##
	- ##[[KeyPgScreengraphics Screen (Graphics)]]##
	- ##[[KeyPgOpVarptr VarPtr]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}