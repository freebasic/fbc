{{fbdoc item="title" value="File I/O with FreeBASIC"}}----

In FreeBASIC, there are 4 possible ways to perform file I/O:

1. Using the built-in BASIC commands like ##[[KeyPgOpen Open]]## , ##[[KeyPgGetfileio Get]]##, ##[[KeyPgPutfileio Put]]##, and ##[[KeyPgClose Close]]##. This way is mostly portable across all platforms supported by FreeBASIC. Open files are identified by "file numbers", that are specific to FreeBASIC and can't be passed into functions from bellow.

2. Using the C stream I/O functions like fopen, fread, ftell, fclose (see Stream I/O in [[ProPgCruntime C Standard Library Functions]]) of the C library FreeBASIC relies on. This is slightly faster than and adds a few features beyond method above, and still well portable. Open files are identified by file pointers, as in the C language, again unique to this access method.  The ##[[KeyPgFileattr FileAttr]]## function can be used to return a stream I/O pointer from a file number as in 1. above.

3. Using the C low-level I/O functions like _open, _read, _write, _close (see Low Level I/O in [[ProPgCruntime C Standard Library Functions]]). Those functions should be portable, but so far headers are available for Win32 only, so code using them will not compile to any other platform by now.

4. Talk directly to the OS kernel (DOS: use DOS and DPMI INT's , Win32: use API calls like ""CreateFile"", ""WriteFile""). This is no longer portable. Files are identified by handles generated by and specific to the OS kernel.

This example shows and compares methods 1. and 2. described above, and reports the values returned by the functions used. It expects 2 commandline arguments, providing names of 2 **different** files with same size allows to compare the reading performance (make sure the file cache is empty before starting test) :

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/fileio/basicvsc.bas"}}%%(freebasic)
Data " File I/O test | 2008-07-04 "
Rem
Rem  Compile with FB 0.18.5
Rem  NOTE: FB 0.20 will support returning the amount of data read when using GET

#include "crt\stdio.bi" '' Otherwise the "C"-stuff won't work

Dim As FILE  Ptr   QQ
Dim As Ubyte Ptr   BUF

Dim As Uinteger    DD, EE, FF, GG, HH
Dim As Ulongint    II64

Dim As String      VGSTEMP, VGSFILE1, VGSFILE2

? : Read VGSTEMP : ? VGSTEMP : ?

VGSTEMP=Command$(1) : VGSFILE1="BLAH"
If VGSTEMP<>"" Then VGSFILE1=VGSTEMP
VGSTEMP=Command$(2) : VGSFILE2=VGSFILE1
If VGSTEMP<>"" Then VGSFILE2=VGSTEMP

BUF = ALLOCATE(32768) '' 32 KiB

? : ? "FB - OPEN - GET """;VGSFILE1;"""": Sleep 1000
FF = Freefile : HH=0 : II64=0
GG=Open (VGSFILE1 For Binary ACCESS READ As #FF) '' Result 0 is OK here
'' "ACCESS READ" prevents from creating an empty file if it doesn't exist yet
? "OPEN: ";GG
If (GG=0) Then
  EE=CAST(Uinteger,(Timer*100)) '' No integer timer in FB
  GG=Get (#FF,,*BUF,32768)
  '' But no way ^^^ to find out how much stuff got read 
  '' Even worse, EOF is __NOT__ considered as error, must vvv use EOF to test
  ? "1st  GET: ";GG
  ? "*** ";BUF[0];" ";BUF[1]
  Do
	HH=HH+1
	If Eof(FF) Or (GG<>0) Then Exit Do
	II64=II64+32768ull
	GG=Get (#FF,,*BUF,32768)
  Loop
  DD=CAST(Uinteger,(Timer*100))-EE
  ? (DD+1)*10;" ms"
  If (HH>1) Then ? "Last GET: ";GG
  ? "Got cca ";II64;" to ";II64+32768ull;" bytes in ";HH;" calls"
  Close #FF
Endif

? : ? "C - FOPEN - FREAD """;VGSFILE2;"""" : Sleep 1000
HH=0 : II64=0
QQ=FOPEN(VGSFILE2,"rb")
'' Here 0 is evil and <>0 good, opposite from above !!!
'' Open existing file, will not be created if it doesn't exist 
'' "rb" is case sensitive and must be lowercase, STRPTR seems not necessary
? "FOPEN: ";QQ 
If (QQ<>0) Then
  EE=CAST(Uinteger,(Timer*100))
  GG=FREAD(BUF,1,32768,QQ) '' 1 is size of byte - can't live without :-D
  '' Returns size of data read, <32768 on EOF, 0 after EOF, or "-1" on error 
  ? "1st FREAD ";GG
  ? "*** ";BUF[0];" ";BUF[1]
  Do
	HH=HH+1
	If (GG<=32768) Then II64=II64+CAST(Ulongint,GG)
	If (GG<>32768) Then Exit Do '' ERR or EOF
	GG=FREAD(BUF,1,32768,QQ)
  Loop
  DD=CAST(Uinteger,(Timer*100))-EE
  ? (DD+1)*10;" ms"
  If (HH>1) Then ? "Last FREAD: ";GG
  ? "Got __EXACTLY__ ";II64;" bytes in ";HH;" calls"
  FCLOSE(QQ)
Endif

DEALLOCATE(BUF): Sleep 1000 '' Crucial

End 
%% 

{{fbdoc item="see"}}
	- [[CatPgFile File I/O Functions]]
	- [[ProPgCruntime C Standard Library Functions]]

{{fbdoc item="back" value="CatPgProgrammer|Programmer's Guide"}}