{{fbdoc item="title" value="WINDOW"}}----
Sets new view coordinates mapping for current viewport

{{fbdoc item="syntax"}}##
	**Window** [ [SCREEN] ( //x1//, //y1// )-( //x2//, //y2// ) ]
##
{{fbdoc item="param"}}
	##SCREEN##
		Optional argument specifying y coordinates increase from top to bottom.
	##( //x1//, //y1// )-( //x2//, //y2// )##
		New floating point values corresponding to the opposite corners of the current viewport.

{{fbdoc item="desc"}}
	##**Window**## is used to define a new coordinates system. ##(//x1//,//y1//)## and ##(//x2//,//y2//)## are the new coordinates to be mapped to the opposite corners of the current viewport; all future coordinates passed to graphics primitive statements will be affected by this new mapping. If ##SCREEN## is omitted, the new coordinates system will be Cartesian, that is, with y coordinates increasing from bottom to top. Call ##**Window**## with no argument to disable the coordinates transformation.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/window.bas"}}%%(freebasic)
screen 13

'' define clipping area
view ( 10, 10 ) - ( 310, 150 ), 1, 15    

'' set view coordinates
window ( -1, -1 ) - ( 1, 1 )             

'' Draw X axis
line (-1,0)-(1,0),7
draw string ( 0.8, -0.1 ), "X"

'' Draw Y axis
line (0,-1)-(0,1),7
draw string ( 0.1, 0.8 ), "Y"

dim as single x, y, s

'' compute step size
s = 2 / pmap( 1, 0 )

'' plot the function
for x = -1 to 1 step s
  y = x ^ 3
  pset( x, y ), 14
next x

'' revert to screen coordinates
window       

'' remove the clipping area
view screen  

'' draw title
draw string ( 120, 160 ), "Y = X ^ 3"

sleep
%%

{{image class="center" title="Window example output" url="/images/window.png"}}

{{fbdoc item="diff"}}
	- None

{{fbdoc item="see"}}
	- ##[[KeyPgScreengraphics Screen (Graphics)]]##
	- ##[[KeyPgViewgraphics View (Graphics)]]##
	- ##[[KeyPgPmap PMap]]##

{{fbdoc item="back" value="CatPgGfxScreen|Screen Functions"}}