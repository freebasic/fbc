{{fbdoc item="title" value="TO"}}----
Statement modifier to specify a range.

{{fbdoc item="syntax"}}##
	FOR //iterator// //intial_value// TO //ending_value//
		//statement(s)//.
	NEXT [ //iterator// ]
//or//
	SELECT CASE //case_comparison_value//
	CASE //lower_bound// TO //upper_bound//
		//statement(s)//.
	END SELECT
//or//
	DIM //variable_identifier//( //lower_bound// TO //upper_bound// ) AS //type_specifier//
##
{{fbdoc item="desc"}}
	The TO keyword is used to define a certain numerical range. This keyword is valid only if used with [[KeyPgFornext FOR ... NEXT]], [[KeyPgSelectcase SELECT / CASE]] and [[KeyPgDim DIM]] statements.

	In the first syntax, the TO keyword defines the initial and ending values of the iterator in a FOR statement.

	In the second syntax, the TO keyword defines lower and upper bounds for CASE comparisons.

	In the third syntax, the TO keyword defines the array bounds in a DIM statement

	For more information, see [[KeyPgFornext FOR...NEXT]], [[KeyPgDim DIM]] and [[KeyPgSelectcase SELECT CASE]].

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/misc/to.bas"}}%%(freebasic)
'' this program uses bound variables along with the TO keyword to create an array, store random
'' temperatures inside the array, and to determine output based upon the value of the temperatures
Randomize Timer

'' define minimum and maximum number of temperatures we will create
Const minimum_temp_count As Integer = 1
Const maximum_temp_count As Integer = 10

'' define the range of temperatures zones in which bacteria breed rapidly (in degrees)
Const min_low_danger As Integer = 40
Const max_low_danger As Integer = 69
Const min_medium_danger As Integer = 70
Const max_medium_danger As Integer = 99
Const min_high_danger As Integer = 100
Const max_high_danger As Integer = 130

'' define array to hold temperatures using our min/max temp count bounds
Dim As Integer array( minimum_temp_count To maximum_temp_count )

'' declare a for loop that iterates from minimum to maximum temp count
Dim As Integer it
For it = minimum_temp_count To maximum_temp_count

   array( it ) = Int( Rnd( 1 ) * 200 ) + 1

   '' display a message based on temperature using our min/max danger zone bounds
   Select Case array( it )
	  Case min_low_danger To max_low_danger
	     Color 11
	     Print "Temperature" ; it ; " is in the low danger zone at" ; array( it ) ; " degrees!"
	  Case min_medium_danger To max_medium_danger
	     Color 14
	     Print "Temperature" ; it ; " is in the medium danger zone at" ; array( it ) ; " degrees!"
	  Case min_high_danger To max_high_danger
	     Color 12
	     Print "Temperature" ; it ; " is in the high danger zone at" ; array( it ) ; " degrees!"
	  Case Else
	     Color 3
	     Print "Temperature" ; it ; " is safe at" ; array( it ) ; " degrees."
   End Select

Next it

Sleep
%%

{{fbdoc item="diff"}}
	- none

{{fbdoc item="see"}}
	- ##[[KeyPgFornext For...Next]]##
	- ##[[KeyPgDim Dim]]##
	- ##[[KeyPgSelectcase Select Case]]##

{{fbdoc item="back" value="CatPgMisc|Miscellaneous"}}